#!bin/zsh

# The path to the directory which contains antidote hooks.
ANTIDOTE_HOOK_DIR=$XDG_CONFIG_HOME/antidote/hooks
# The name of the bundle file antidote use to load plugins.
ANTIGEN_BUNDLE_FILE=$XDG_CONFIG_HOME/antidote/.zsh_plugins.txt
# The name of the static file antidote will generate.
ANTIGEN_STATIC_FILE=$XDG_DATA_HOME/antidote/.zsh_plugins.zsh

# Override defualt bundle / static file
# See: `man antidote-load`
zstyle ':antidote:bundle' file $ANTIGEN_BUNDLE_FILE
zstyle ':antidote:static' file $ANTIGEN_STATIC_FILE

# Use a more friendly name for plugin cache directories
# See: https://getantidote.github.io/options#friendly-names
zstyle ':antidote:bundle' use-friendly-names 'yes'

# Ensure we have a bundle file where we can add plugins.
# Only create the file if it does not exist.
# Otherwise, antidote bundle will always be executed according to the condition we write.
[[ -f $ANTIGEN_BUNDLE_FILE ]] || touch $ANTIGEN_BUNDLE_FILE

# Lazy-load antidote.
fpath+=($(brew --prefix)/opt/antidote/share/antidote/functions)
autoload -Uz $fpath[-1]/antidote

# Generate static file in a subshell when bundle file is updated.
if [[ ! $ANTIGEN_STATIC_FILE -nt $ANTIGEN_BUNDLE_FILE ]]; then
  antidote bundle < $ANTIGEN_BUNDLE_FILE >| $ANTIGEN_STATIC_FILE
fi

# Load pre / post hook functions BEFORE sourcing static plugins file
#
# Hook rules:
# 1. Hook should be placed in the directory `$ANTIDOTE_HOOK_DIR/[PLUGIN_NAME]`.
#    e.g. `$ANTIDOTE_HOOK_DIR/zsh-autosuggestions` for plugin `zsh-users/zsh-autosuggestions`
# 2. If PLUGIN_NAME is duplicated, use `@[NAME_SPACE]` to distinguish each other.
#    e.g. `completion@foo` for plugin `foo/completion`, and `completion@bar` for plugin `bar/completion`
# 3. Hook should have file name in the either one of the following format:
#    - `plugin::{pre,post}-hook.zsh`
#    - `plugin::path@to@plugin::{pre,post}-hook.zsh`
#    - `custom::name-of-custom-plugin::{pre,post}-hook.zsh`
# 4. Hook function should be named descriptively
#
# `(N)` means to set the `NULL_GLOB` option for the current pattern, so unmatched pattern will be deleted.
for ANTIDOTE_HOOK_FILE ($ANTIDOTE_HOOK_DIR/*/{custom,plugin}(::|::*::){pre,post}-hook.zsh(N)); do
  source $ANTIDOTE_HOOK_FILE
done
unset ANTIDOTE_HOOK_FILE

# Source the static plugins file.
source $ANTIGEN_STATIC_FILE
